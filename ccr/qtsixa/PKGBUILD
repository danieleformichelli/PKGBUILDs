# Maintainer:  danyf90 <daniele.formichelli@gmail.com>
# Contributor: Mattias Andr√©e <maandree@operamail.com>

pkgname=qtsixa
pkgver=1.5.1
pkgrel=1
pkgdesc="An utility to configure and use sixaxis controller via bluetooth."
url="http://sourceforge.net/projects/qtsixa/"
arch=('x86_64')
license=('GPL')
install=qtsixa.install
backup=('etc/default/sixad')
depends=('bluez' 'pyqt' 'qt' 'python2' 'bluez-hcidump' 'jack' 'libusb-compat')
makedepends=('patch' 'pyqt')
source=("http://downloads.sourceforge.net/qtsixa/QtSixA-$pkgver-src.tar.gz"
      "sixad-init.patch"
      "sixad-bin.patch"
      "sixad-shared-h.patch"
      "sixad.service"
      "qtsixa-dbus.patch"
      "qtsixa-gksudo.patch")
sha512sums=('53e8988f3eac730fc116347d32ed45c975bb176b4a4e9b13b3854db735d64c5de32be9fd845ee84d860f025aa60c01776a7ad1f2eca74c6a8a137e132c9239fe'
        'd0c6826353d85a1b928910c548a5ee8cc965284433af4916226694f6c33baf90d1b7f5f284f1570f39b242e75cd3ae2e254b46bae6e77fb699dd25831ca7c8bc'
        '181d3a722257faeec9c81da548b9a601a2955e10c1043c43c136f5d40e8493d2be3f92e13f63c9b6d99a268c1bfe9f1daaf342e5bf643c6d9ede8920ad0160fa'
        '873e201f5909139f2789fefc0e0d03752f0582f333240267bcbce91726ef8b9cc217212ff66386e8f52f2ab2b47b41f8ec36d4e15ffec9466e3ea58e9860268d'
        'fd97238c3af90965afbd6418edba7bc70a8775ea4097ac60d7fea2d00076c6bce7ea75a5829f10a0d910c311f21051621a225e7c3b9d4c6ae813fa066cbe3ddb'
        '6b201ff415638bd7d324f95422ae6ed6b7037cf1a4d545dbe102fe71cbebedd6eab1d6cc61bafef12da8637d0a1e2166986d3b29fa8069a70a3711993f5bef86'
        'c786b59960a3a44f694237c5bca712d612d8797d80617edb3d833a2b29b3933548933910d3776e0954226ae7ae1c4cd13110031722fe8635563700d22a999f12')

build() {
  cd $srcdir

  # patch for sixad daemon
  echo "Patching sixad..."
  patch -p0 < $srcdir/sixad-init.patch
  patch -p0 < $srcdir/sixad-bin.patch
  patch -p0 < $srcdir/sixad-shared-h.patch
  patch -p0 < $srcdir/qtsixa-dbus.patch
  patch -p0 < $srcdir/qtsixa-gksudo.patch

  cd $srcdir/QtSixA-$pkgver/qtsixa/gui/
  echo "Fixed python version..."
  find . -name "*.py" -exec sed -i "s/python/python2/g" '{}' \;
  sed -i 's/python/python2/g' ../qtsixa
  sed -i 's/python/python2/g' ../../sixad/sixad-dbus-blocker
  echo "Fixed bluetooth daemon"
  sed -i 's/init.d/rc.d/g' qtsixa_main.py

  cd $srcdir/QtSixA-$pkgver

  make all || exit 1
}

package() {
  cd $srcdir/QtSixA-$pkgver

  ## Qtsixa installation, adapted from makefile of qtsixa folder ##
  ## make directories
  install -d $pkgdir/{usr/{bin,share/{applications,pixmaps,qtsixa/{game-profiles,gui,icons,pics,profiles}}}}

  ##systemd
  install -d $pkgdir/usr/lib/systemd/system/
  install -m 644 $srcdir/sixad.service $pkgdir/usr/lib/systemd/system/

  cd qtsixa

  ## Install files
  install -m 755 qtsixa $pkgdir/usr/bin/
  install -m 755 sixad-lq $pkgdir/usr/bin/
  install -m 755 sixad-notify $pkgdir/usr/bin/
  #install -m 644 manual/* $pkgdir/usr/share/doc/qtsixa/manual/
  install -m 644 game-profiles/* $pkgdir/usr/share/qtsixa/game-profiles/
  install -m 644 gui/*.py $pkgdir/usr/share/qtsixa/gui/
  install -m 644 icons/* $pkgdir/usr/share/qtsixa/icons/
  install -m 644 pics/* $pkgdir/usr/share/qtsixa/pics/
  install -m 644 profiles/* $pkgdir/usr/share/qtsixa/profiles/
  #install -m 644 lang/* $pkgdir/usr/share/qtsixa/lang/
  install -m 644 sixad-notify.desktop $pkgdir/usr/share/qtsixa/
  install -m 644 qtsixa.desktop $pkgdir/usr/share/applications/
  install -m 644 qtsixa.xpm $pkgdir/usr/share/pixmaps/

  ## Sixad installation, adapted from makefile of sixad folder ##
  ## Make directories

  install -d \
    $pkgdir/etc/default/ \
    $pkgdir/etc/rc.d/ \
    $pkgdir/etc/logrotate.d/ \
    $pkgdir/usr/sbin/ \
    $pkgdir/var/lib/sixad/ \
    $pkgdir/var/lib/sixad/profiles/

  chmod 775 -R $pkgdir/var/lib/sixad/

  cd ../sixad

  # Install files
  install -m 644 sixad.default $pkgdir/etc/default/sixad
  install -m 755 sixad.init $pkgdir/etc/rc.d/sixad
  install -m 644 sixad.log $pkgdir/etc/logrotate.d/sixad
  install -m 755 sixad $pkgdir/usr/bin/
  install -m 755 bins/sixad-bin $pkgdir/usr/sbin/
  install -m 755 bins/sixad-sixaxis $pkgdir/usr/sbin/
  install -m 755 bins/sixad-remote $pkgdir/usr/sbin/
  install -m 755 bins/sixad-3in1 $pkgdir/usr/sbin/
  install -m 755 bins/sixad-raw $pkgdir/usr/sbin/
  install -m 755 sixad-dbus-blocker $pkgdir/usr/sbin/
  #chmod 777 -R $pkgdir/var/lib/sixad/

  ## Utils installation, adapted from makefile of utils folder ##
  ## Install files

  cd ../utils
  install -m 755 bins/sixpair $pkgdir/usr/sbin/
  install -m 755 bins/sixpair-kbd $pkgdir/usr/sbin/
  install -m 755 bins/hidraw-dump $pkgdir/usr/sbin/
  install -m 755 bins/sixad-jack $pkgdir/usr/bin/

}