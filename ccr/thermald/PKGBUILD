# Maintainer:  danyf90 <daniele.formichelli@gmail.com>
# Contributor: wallnuss <v dot churavy at gmail dot com
# Contributor: WonderWoofy <sugar.and.scruffy@gmail.com>

pkgname=thermald
pkgver=1.2
pkgrel=1
pkgdesc="The Linux Thermal Daemon program from 01.org"
arch=('x86_64')
url="https://github.com/01org/thermal_daemon"
license=('GPL2')
depends=('dbus-glib>=0.94' 'libxml2>=2.4')
backup=('etc/thermald/thermal-conf.xml')
makedepends=('git')
source=($url/archive/v$pkgver.tar.gz
        'modules-load-msr.conf')
sha512sums=('deabccb9cdb95c9b61240e40f14c6e24dad156e4e13dd22876d33cf2f19f0fa8498da48fe09338d2651b8d235559c466c839c531bfbf8b635d255ba29fa3624f'
            '3439580434862c6207887d23917e031d1efccc9e4b070599bd42f5f05e7a43780e504faec0b0fcfbd3d3c581f22d5c32a2f0da9e8068f8ee0fd3147f5df983e2')

build() {
  cd thermal_daemon-$pkgver

  ./autogen.sh
  ./configure --prefix=/usr --sysconfdir=/etc
  make
}

package() {
  cd thermal_daemon-$pkgver

  make DESTDIR=$pkgdir install
  install -Dm644 $srcdir/modules-load-msr.conf $pkgdir/usr/lib/modules-load.d/thermald.conf
}
