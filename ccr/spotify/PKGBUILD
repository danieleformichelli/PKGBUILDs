# Maintainer:  danyf90 <daniele.formichelli@gmail.com>
# Contributor: Gadget3000 <gadget3000@msn.com>
# Contributor: Eothred <yngve.levinsen@gmail.com>

pkgname=spotify
pkgver=0.9.11.27
_pkgver=g2b1a638.81-1
pkgrel=2
pkgdesc="A proprietary peer-to-peer music streaming service"
arch=('x86_64')
license=('custom:"Copyright (c) 2006-2013 Spotify Ltd"')
install=$pkgname.install
url="http://www.spotify.com"
options=('!strip')
depends=("alsa-lib" "gconf" "glibc" "libgcrypt15" "libxss" "qtwebkit" "nspr<5.0")
optdepends=('desktop-file-utils: Adds URI support to compatible desktop environments'
            'ffmpeg-compat: Adds playback support for local files')
source=("http://repository-origin.spotify.com/pool/non-free/s/$pkgname/$pkgname-client_$pkgver.${_pkgver}_amd64.deb"
        "$pkgname"
        "$pkgname.protocol")
sha512sums=('edb28e0e08aadf354277c92dc3421ece96370f2938e7a041b4f20b56515d9c5641d4483f05800758c30944252df5552e1287f1ecf71961e8e33a11f815a594ed'
            'd952b02d089c3ffc493c866386b4f2af857bb11c0bcb63a38107997513395aa2a93dc7eb9099a3e21aec468809f2bd583e4512f75c3c56367c5767ee47e09bb9'
            '999abe46766a4101e27477f5c9f69394a4bb5c097e2e048ec2c6cb93dfa1743eb436bde3768af6ba1b90eaac78ea8589d82e621f9cbe7d9ab3f41acee6e8ca20')


package() {
  cd $srcdir

  ar x $pkgname-client_$pkgver.${_pkgver}_amd64.deb > /dev/null
  tar -xzf data.tar.gz -C $pkgdir

  install -d $pkgdir/usr/share/
  mv $pkgdir/opt/spotify $pkgdir/usr/share/
  rm -r $pkgdir/opt

  # Bin Script
  install -Dm755 $srcdir/spotify $pkgdir/usr/bin/spotify

  # libudev.so
  install -d $pkgdir/usr/share/spotify/libs/
  ln -s /usr/lib/libudev.so $pkgdir/usr/share/spotify/libs/libudev.so.0

  # Copy license
  install -Dm644 $pkgdir/usr/share/doc/$pkgname-client/copyright $pkgdir/usr/share/licenses/$pkgname/copyright

  install -Dm644 $srcdir/spotify.protocol $pkgdir/usr/share/kde4/services/spotify.protocol
}
