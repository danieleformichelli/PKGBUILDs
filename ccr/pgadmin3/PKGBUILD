# Maintainer:  danyf90 <daniele.formichelli@gmail.com>
# AUR Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Phil Miller <philm[at]chakra-project[dot]org

pkgname=pgadmin3
pkgver=1.22.1
pkgrel=1
pkgdesc="A comprehensive design and management interface for PostgreSQL database"
arch=('x86_64')
url="http://www.pgadmin.org"
license=('custom')
depends=('wxgtk' 'postgresql-libs' 'libxslt')
makedepends=('libpqxx')
install=pgadmin3.install
source=("http://ftp.postgresql.org/pub/pgadmin3/release/v$pkgver/src/pgadmin3-$pkgver.tar.gz")
sha512sums=('f190e82317e10b75cb0ebd7cc0c878c4be9c392e33aba44a1356446e4cac92735834a32b1d65999ffc3e36ed130d4cfcc1c25ad5faa3c745e884fccd797331e0')

prepare() {
  cd $srcdir
  convert pgadmin3-$pkgver/pgadmin/include/images/pgAdmin3.ico pgAdmin3.png

  cd pgadmin3-$pkgver
  sed -i 's/wx-config/wx-config-2.8/' configure
  sed -i 's/wxrc/wxrc-2.8/g' stringextract pgadmin/ui/embed-xrc
}

build() {
  cd pgadmin3-$pkgver
  [ -f Makefile ] || ./configure --prefix=/usr --with-wx-version=2.8
  make
}

package() {
  cd $srcdir/pgadmin3-$pkgver

  make DESTDIR=$pkgdir/ install
  install -Dm644 i18n/$pkgname.lng $pkgdir/usr/share/pgadmin3/i18n
  install -Dm644 LICENSE $pkgdir/usr/share/licenses/$pkgname/LICENSE

  install -Dm644 pgadmin/include/images/pgAdmin3.ico $pkgdir/usr/share/pgadmin3/pgAdmin3.ico
  install -Dm644 $srcdir/pgAdmin3-1.png $pkgdir/usr/share/pgadmin3/pgAdmin3.png

  install -Dm644 $srcdir/pgAdmin3-3.png $pkgdir/usr/share/icons/hicolor/16x16/apps/pgAdmin3.png
  install -Dm644 $srcdir/pgAdmin3-2.png $pkgdir/usr/share/icons/hicolor/32x32/apps/pgAdmin3.png
  install -Dm644 $srcdir/pgAdmin3-1.png $pkgdir/usr/share/icons/hicolor/48x48/apps/pgAdmin3.png

  install -Dm644 pkg/pgadmin3.desktop $pkgdir/usr/share/applications/pgadmin3.desktop
}
