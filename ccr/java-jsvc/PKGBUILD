# Maintainer: Guillaume ALAUX <guillaume@archlinux.org>

pkgname=java-jsvc
pkgver=1.0.15
pkgrel=1
_libname=commons-daemon
pkgdesc='Apache application for making Java applications run on UNIX more easily'
depends=("java-${_libname}" 'libcap')
arch=('x86_64')
url='http://commons.apache.org/daemon/'
license=('APACHE')
source=("http://archive.apache.org/dist/commons/daemon/source/$_libname-$pkgver-src.tar.gz")
sha512sums=('f20cc8796978fb09b6ecc8d99bb3c8b271b097d74c1f99b286cde213631e7f50a4104f5db2c354f9624ba8b037502d81e327c581b38253b9b216aa2172016e40')
makedepends=('apache-ant' 'docbook2x' 'docbook-xsl')

build() {
  cd $srcdir/$_libname-$pkgver-src/src/native/unix

  ./configure
  make
  docbook2man man/jsvc.1.xml
}

package() {
  cd $srcdir/$_libname-$pkgver-src/src/native/unix

  install -Dm755 jsvc $pkgdir/usr/bin/jsvc
  install -Dm644 JSVC.1 $pkgdir/usr/share/man/man1/JSVC.1
}

