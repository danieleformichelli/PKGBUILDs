# Maintainer:  danyf90 <daniele.formichelli@gmail.com>
# Contributor: Guido <qqqqqqqqq9 at web dot de>
# Contributor: Limao Luo <luolimao+AUR at gmail com>
# Contributor: Alexander Fehr <pizzapunk at gmail dot com>
# Contributor: Jose Valecillos <valecillosjg at gmail dot com>

pkgname=acroread
pkgver=9.5.5
pkgrel=5
pkgdesc="Adobe Reader is a PDF file viewer"
arch=('x86_64')
url="http://www.adobe.com/products/reader/"
license=(custom)
depends=('desktop-file-utils' 'lib32-gtk2' 'lib32-libidn' 'lib32-libxml2' 'lib32-libxt' 'lib32-mesa' 'lib32-pangox-compat')
optdepends=('acroread-fonts: CJK and extended font packs' 'lib32-libcanberra: XDG sound support')
makedepends=('bsdiff')
options=(!strip)
install=$pkgname.install
source=("http://ardownload.adobe.com/pub/adobe/reader/unix/9.x/$pkgver/enu/AdbeRdr$pkgver-1_i486linux_enu.bin"
        "nppdf.bsdiff"
        "$pkgname.desktop")
sha512sums=('f9c18ca0dc0687de353afeb90925439a557a14604e6361ff2b229ec29257ff051ea9eac6a90671b38a745473b5c87135377d035520f441ceaabf2510d690675b'
            '253def4898cc2469ef5ed33630302e9da4a65707297f2ecc207d7467e7369e4f1014aa77aac93538ea0682dd4c2eee408b7632a319d1ba850fb49ba608fde6de'
            'f4e4ee93ee22dfcfe98df27e1f2a77f84e2a0cc184f148939bc6e30a2f2d49a35ee74c77aedda17077774c26b57394bed211ca850fb666a07208d71e41ac7140')

prepare() {
  # Extracting content from bin file
  tail -c+6889 AdbeRdr9.5.5-1_i486linux_enu.bin | xz -qqd | tar x
}

build() {
  cd AdobeReader/
  bsdtar -xf COMMON.TAR
  bsdtar -xf ILINXR.TAR

  sed -i "s/_filedir/&_acroread/" Adobe/Reader9/Resource/Shell/acroread_tab

  # Bug on log created by Adobe Reader browser plugin https://bugs.launchpad.net/ubuntu/+source/acroread/+bug/986841
  bspatch Adobe/Reader9/Browser/intellinux/nppdf.so Adobe/Reader9/Browser/intellinux/nppdf.so ../nppdf.bsdiff
}

package() {
  desktop-file-install $pkgname.desktop --dir $pkgdir/usr/share/applications/
  cd AdobeReader/Adobe/Reader9/

  msg2 "Installing Main Files..."
  install -d $pkgdir/opt/Adobe/Reader9
  cp -a * $pkgdir/opt/Adobe/Reader9/

  msg2 "Installing Bin Files..."
  install -d $pkgdir/usr/bin/
  ln -s /opt/Adobe/Reader9/bin/acroread $pkgdir/usr/bin/
  install -Dm644 Resource/Shell/acroread.1.gz $pkgdir/usr/share/man/man1/acroread.1.gz
  install -Dm644 Resource/Shell/acroread_tab $pkgdir/etc/bash_completion.d/acroread

  msg2 "Installing Broswer Plugin..."
  install -d $pkgdir/usr/lib/mozilla/plugins/
  ln -s /opt/Adobe/Reader9/Browser/intellinux/nppdf.so $pkgdir/usr/lib/mozilla/plugins/

  msg2 "Installing Icon Resources..."
  install -Dm644 Resource/Icons/64x64/AdobeReader9.png $pkgdir/usr/share/pixmaps/$pkgname.png

  msg2 "Installing License..."
  install -Dm644 Reader/Legal/en_US/License.txt $pkgdir/usr/share/licenses/$pkgname/LICENSE
}
