# Maintainer:  danyf90 <daniele.formichelli@gmail.com>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Christian Himpel <chressie at gmail dot com>
# Contributor: Johannes Hanika <hanatos at gmail dot com>

pkgname=darktable
pkgver=1.4.2
_pkgver=1.4
pkgrel=1
pkgdesc="Utility to organize and develop raw images"
arch=('x86_64')
url="http://darktable.sf.net/"
license=('GPL3')
depends=('cairo' 'exiv2>=0.18' 'intltool>=0.40' 'lcms2' 'lensfun>=0.2.3' 'libglade' 'dbus-glib'
	 'curl' 'libgnome-keyring' 'libgphoto2' 'openexr' 'sqlite3' 'libxslt'
	 'libsoup' 'gtk-engines' 'json-glib' 'flickcurl')
makedepends=('intltool>=0.40' 'cmake' 'librsvg')
optdepends=('librsvg')
install=$pkgname.install
source=("http://downloads.sourceforge.net/project/darktable/darktable/$_pkgver/darktable-$pkgver.tar.xz")
sha512sums=('5a4ba7f5de75bb4db31861fbc095e7854bf701211377b42bbbc3da00a57bcf5aa6e81745b5f34c1c6c7d9a6a3a43678db3d8373d556587e2d3c9210193163f9e')

build() {
  install -d $srcdir/$pkgname-$pkgver/build
  cd $srcdir/$pkgname-$pkgver/build
  CXXFLAGS+=" -fpermissive"
  cmake \
      -DCMAKE_INSTALL_PREFIX=/usr \
      -DCMAKE_BUILD_TYPE=Release \
      -DDONT_INSTALL_GCONF_SCHEMAS=True \
      -DBINARY_PACKAGE_BUILD=1 \
      -DUSE_GCONF_BACKEND=Off \
      -DBUILD_USERMANUAL=False \
      ..
  make
}

package() {
  cd $srcdir/$pkgname-$pkgver/build
  make DESTDIR=$pkgdir install
  mv $pkgdir/usr/share/doc/darktable $pkgdir/usr/share/doc/$pkgname-$pkgver
}
