# Maintainer:  danyf90 <daniele.formichelli@gmail.com>
# Contributor: Maxime Gauduin <alucryd@gmail.com>
# Contributor: josephgbr <rafael.f.f1@gmail.com>
# Contributor: vEX <vex@niechift.com>

pkgname=pcsx2
pkgver=1.2.2
pkgrel=1
pkgdesc="A Sony PlayStation 2 emulator"
arch=('x86_64')
url="http://www.pcsx2.net"
license=('GPL')
depends=('lib32-glew' 'lib32-libaio' 'lib32-libcanberra' 'lib32-libjpeg-turbo' 'lib32-mesa' 'lib32-libgl' 'lib32-nvidia-cg-toolkit' 'lib32-portaudio' 'lib32-sdl2' 'lib32-soundtouch' 'lib32-wxgtk')
makedepends=('cmake' 'gcc-multilib')
options=('!emptydirs')
source=("https://github.com/PCSX2/$pkgname/archive/v$pkgver.tar.gz")
sha512sums=('d8e7f1d76cafb5269a74324befd740df07a7ce92d57cfcceab6f392ef613882a7c2e9568124d90510f953380324df525fdcd12c0696957bcaf4dc5006dfd3a22')

build() {
  cd $pkgname-$pkgver

  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build && cd build

  export CC='gcc -m32'
  export CXX='g++ -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'
  cmake .. -DCMAKE_INSTALL_PREFIX='/usr' -DCMAKE_BUILD_TYPE='Release' -D{GLSL_API,PACKAGE_MODE,REBUILD_SHADER,SDL2_API,XDG_STD}=TRUE -DPLUGIN_DIR='/usr/lib32/pcsx2' -DGAMEINDEX_DIR='/usr/share/pcsx2' -DwxWidgets_CONFIG_EXECUTABLE='/usr/bin/wx-config32-2.8' -DwxWidgets_wxrc_EXECUTABLE='/usr/bin/wxrc32-2.8' -DCMAKE_LIBRARY_PATH='/usr/lib32'

  make
}

package() {
  cd $pkgname-$pkgver/build

  make DESTDIR=$pkgdir install
}
