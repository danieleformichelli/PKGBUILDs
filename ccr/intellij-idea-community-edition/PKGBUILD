# Maintainer:  danyf90 <daniele.formichelli@gmail.com>
# Contributor: Lukas Jirkovsky <l.jirkovsky@gmail.com>

pkgname=intellij-idea-community-edition
pkgver=14.0.1
_pkgver=139.225.3
pkgrel=1
pkgdesc="IDE for Java, Groovy and other programming languages with advanced refactoring features"
arch=('any')
url="http://www.jetbrains.org/"
license=('Apache')
depends=('java-environment' 'giflib' 'libxtst')
install=idea.install
source=("http://download.jetbrains.com/idea/ideaIC-$pkgver.tar.gz"
        "idea.desktop"
        "idea.sh")
sha512sums=('f42e612269d1d300071e3a1634fcc4e47a3e2d788ec07ce8962812163ba71d6b0e83dc643ca92672a6bdb829e95470e7e2717ddfbe3ec0a37f6737bfd4ac66c7'
            '0dae33ea761ba32b911103b7b54462deeae7b3ac54b95c3b177ec3386451269ba2f4bcf92329e13edfa1ce674c68003453591d06e8eab9443a39b1791e9b2444'
            'bafedcce57d9ba3ed19421c513d40789da586598eacccd013ce6652fd62b0c6b5cf0c10c3738b37e876f20e1b743f09e3cc74ec4510a491ef894aa42fc4c096b')

package() {
  install -dm755 $pkgdir/usr/share
  cp -a idea-IC-$_pkgver $pkgdir/usr/share/intellijidea-ce

  # make sure that all files are owned by root
  chown -R root:root $pkgdir/usr/share

  # never wait on user input when starting idea
  sed -i '/.*read IGNORE.*/ d' $pkgdir/usr/share/intellijidea-ce/bin/idea.sh

  install -D -m755 $srcdir/idea.sh $pkgdir/usr/bin/idea.sh
  install -D -m644 $srcdir/idea.desktop $pkgdir/usr/share/applications/idea.desktop
  install -D -m644 $pkgdir/usr/share/intellijidea-ce/bin/idea.png $pkgdir/usr/share/pixmaps/idea.png

  # workaround FS#40934
  sed -i 's|lcd|on|' $pkgdir/usr/share/intellijidea-ce/bin/*.vmoptions
}
