# Maintainer:  danyf90 <daniele.formichelli@gmail.com>
# Contributor: Stephen McIntosh <stephenmac7@gmail.com>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Douglas Soares de Andrade <douglas@archlinux.org>

pkgname=source-highlight
pkgver=3.1.7
pkgrel=2
pkgdesc="Convert source code to syntax highlighted document"
arch=('x86_64')
url="http://www.gnu.org/software/src-highlite/"
license=('GPL')
depends=('bash' 'boost-libs')
makedepends=('ctags' 'boost')
install=$pkgname.install
source=("ftp://ftp.gnu.org/gnu/src-highlite/$pkgname-$pkgver.tar.gz")
md5sums=('0ff81588d3536b4c4e571122ba940595')

build() {
  cd $srcdir/$pkgname-$pkgver

  ./configure --prefix=/usr --sysconfdir=/etc --with-bash-completion=$pkgdir/usr/share/bash-completion/completions
  make
}

package() {
  cd $srcdir/$pkgname-$pkgver

  make prefix=$pkgdir/usr bash_completiondir=$pkgdir/usr/share/bash-completion/completions install

  # PKGBUILD syntax highlighting
  echo -e "\npkgbuild = sh.lang" >> $pkgdir/usr/share/$pkgname/lang.map
}
