# Maintainer:  danyf90 <daniele.formichelli@gmail.com>
# Contributor: Gaetan Bisson <bisson@archlinux.org>
# Contributor: damir <damir@archlinux.org>

pkgname=x11vnc
pkgver=0.9.13
pkgrel=1
pkgdesc='VNC server for real X displays'
url='http://www.karlrunge.com/x11vnc/'
arch=('x86_64')
license=('GPL2')
depends=('openssl' 'libjpeg' 'libxtst' 'libxinerama' 'libxdamage' 'libxrandr' 'avahi')
optdepends=('tk: for GUI support')
source=("http://downloads.sourceforge.net/project/libvncserver/$pkgname/$pkgver/$pkgname-$pkgver.tar.gz"
        'service')
sha512sums=('7b24e495f29122032e1c8ced7f7d8e0a56a2a8263e761a50725320742c99a0fcd5680d13038dd5d47e1c05d3b889d0caa13ba0b1aeda6fa42d5fb5ef3c43f908'
            'c7a80ab01092b3cbc18768b8b2fa5ca5b57e90f1932fe6bbdf3026de5f9234864bc1a4a40fed54cf3b9662523c705cfc39ecffdcb563e9cf4fe2e25c9bbb0ee5')

build() {
  cd $srcdir/$pkgname-$pkgver
  ./configure --prefix=/usr --mandir=/usr/share/man
  make
}

package() {
  cd $srcdir/$pkgname-$pkgver
  make DESTDIR=$pkgdir install
  install -Dm644 $srcdir/service $pkgdir/usr/lib/systemd/system/x11vnc.service
  rm -rf $pkgdir/usr/include/rfb # provided by libvncserver
}
