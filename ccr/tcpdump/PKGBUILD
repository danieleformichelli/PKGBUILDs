# Maintainer:  danyf90 <daniele.formichelli@gmail.com>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: dorphell <dorphell@archlinux.org>

pkgname=tcpdump
pkgver=4.7.3
pkgrel=1
pkgdesc="A tool for network monitoring and data acquisition"
arch=('x86_64')
url="http://www.tcpdump.org"
license=('BSD')
depends=('libpcap' 'openssl')
source=("http://www.tcpdump.org/release/$pkgname-$pkgver.tar.gz"
        "tcpdump_duplicate_executable.patch")
sha512sums=('3363e4f8f39a5c409d3e645216c6c20b1e61d3c98e42847c5156a45c1657f2e52a61fecc519f68a584981b97dff1468ff75f570b52a127f505edea87cc6b1c65'
            '47836a5e144e8e087aa58987f821f92bac2a6c58d29990b28ab33b7082cd476fa98473801d32cca1f1c27415a9a7552e7125809dece12597dc6ddd13a02d1ed9')

prepare() {
  cd $srcdir/$pkgname-$pkgver

  # do not install tcpdump-4.1.1 executable (FS#23346)
  patch -Np1 -i $srcdir/tcpdump_duplicate_executable.patch
}

build() {
  cd $srcdir/$pkgname-$pkgver

  ./configure --prefix=/usr --sbindir=/usr/bin --enable-ipv6
  make
}

package() {
  cd $srcdir/$pkgname-$pkgver

  make DESTDIR=$pkgdir install

  #install the license
  install -D -m644 LICENSE $pkgdir/usr/share/licenses/$pkgname/LICENSE
}
