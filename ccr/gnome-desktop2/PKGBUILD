# Maintainer:  danyf90 <daniele.formichelli@gmail.com>
# Contributor: Jan "heftig" Steffens <jan.steffens@gmail.com>
# Jan de Groot <jan@archlinux.org>

_pkgname=gnome-desktop
pkgname=${_pkgname}2
pkgver=2.32.1
pkgrel=2
pkgdesc="Library with common API for various GNOME modules (legacy version)"
arch=('x86_64')
license=('GPL' 'LGPL')
depends=('gconf' 'gtk2' 'startup-notification')
makedepends=('gnome-doc-utils' 'intltool')
url="http://www.gnome.org"
options=('!emptydirs')
source=("http://ftp.gnome.org/pub/gnome/sources/$_pkgname/${pkgver%.*}/$_pkgname-$pkgver.tar.bz2")
sha512sums=('b8084d08366dab661a7ee369cd03ba62beddbdfa9a36b988b7d13d207b33d6916f7fa8691717fba7b6a70d8276779e35360b1dc385e6ec876f30ea7b8b373c28')

build() {
  cd $srcdir/$_pkgname-$pkgver

  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --disable-static \
      --with-gnome-distributor="Archlinux" \
      --disable-scrollkeeper \
      --disable-gnome-about --disable-desktop-docs

  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0 /g' -e 's/    if test "$export_dynamic" = yes && test -n "$export_dynamic_flag_spec"; then/      func_append compile_command " -Wl,-O1,--as-needed"\n      func_append finalize_command " -Wl,-O1,--as-needed"\n\0/' libtool

  make
}

package() {
  cd $srcdir/$_pkgname-$pkgver

  make DESTDIR=$pkgdir install
}
