# Maintainer : danyf90 <daniele.formichelli@gmail.com>
# Contributor: Stephen McIntosh <stephenmac7@gmail.com>
# Committer:   Manolis Tzanidakis <manolis@archlinux.org>
# Contributor: Simon Lackerbauer <calypso "at" strpg.org>

pkgname=phpmyadmin
pkgver=4.1.5
pkgrel=1
pkgdesc='A PHP and hence web-based tool to administrate MySQL over the WWW'
arch=('any')
url="http://www.phpmyadmin.net"
license=('GPL')
depends=('mariadb-clients' 'php')
optdepends=('php-mcrypt: to use phpMyAdmin internal authentication')
backup=('etc/webapps/phpmyadmin/config.inc.php')
source=("http://downloads.sourceforge.net/sourceforge/$pkgname/phpMyAdmin-$pkgver-all-languages.tar.bz2")
sha512sums=('5872d76eed3b9f6b9e5f70bd6bf569a05b2366a32bb99534383d9ce7944a699b001675d0c9785ea62bb7290be875504612c1476783029b466ebc3d7acc7c6e53')

package() {
  _instdir=$pkgdir/usr/share/webapps/phpMyAdmin
  install -d $_instdir $pkgdir/etc/webapps/phpmyadmin

  cp -ra $srcdir/phpMyAdmin-$pkgver-all-languages/* $_instdir

  ln -s /etc/webapps/phpmyadmin/config.inc.php $_instdir/config.inc.php
  cp $_instdir/config.sample.inc.php $pkgdir/etc/webapps/phpmyadmin/config.inc.php

  cat > $pkgdir/etc/webapps/phpmyadmin/apache.example.conf <<EOF
  Alias /phpmyadmin "/usr/share/webapps/phpMyAdmin"
  <Directory "/usr/share/webapps/phpMyAdmin">
    AllowOverride All
    Options FollowSymlinks
    Order allow,deny
    Allow from all
    php_admin_value open_basedir "/srv/:/tmp/:/usr/share/webapps/:/etc/webapps:/usr/share/pear/"
  </Directory>
EOF
}