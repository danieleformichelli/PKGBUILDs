# Maintainer:  danyf90 <daniele.formichelli@gmail.com>
# Contributor: Samir Benmendil <samir.benmendil[at]gmail[dot]com>
# Contributor: jbitcm- <chakradeveloper@gmail.com>

pkgname=aptana-studio
_pkgname=aptana
pkgver=3.4.2
pkgrel=1
pkgdesc="A professional, open source development tool for the open web."
arch=("x86_64")
url="http://www.aptana.com/"
license=("GPLv3")
depends=('gtk2' 'gconf' 'java-runtime' 'libjpeg-turbo' 'libpng12' 'net-tools')
makedepends=("unzip")
options=(!strip)
replaces=("aptana")
conflicts=("aptana")
source=("http://download.aptana.com/studio3/standalone/${pkgver}/linux/Aptana_Studio_3_Setup_Linux_x86_64_${pkgver}.zip"
        "aptana.desktop")
sha512sums=('d0dc7915c47c9734847fcc7399f9fb10364674e4f79443c0f469df8d64690e0241d8b935c5d6f9d75af3499fcbb2c5693a72793d96957b9b45fc75ca67177f5b'
            '4a18b8d672c8a7d0ecba298b4ded1d6a0ab95425d7361371013b45af70d62b9f93f0058339a5f542a5fef9439a61f3bcfeb1d59327d22e102c09ef6d6eb22a35')

package() {
  cd $srcdir/Aptana_Studio_3

  local _instpath=/usr/share/$_pkgname

  # Install Dirs
  find . -type d -exec install -d "{}" "$pkgdir/$_instpath/{}" \;

  # Install Files
  find . -type f -perm 644 -exec install -Dm644 "{}" "$pkgdir/$_instpath/{}" \;

  # Install Executables
  find . -type f -perm 755 -exec install -Dm755 "{}" "$pkgdir/$_instpath/{}" \;

  mkdir -p $pkgdir/usr/share/applications
  cp $srcdir/aptana.desktop $pkgdir/usr/share/applications/
}