# Maintainer:  danyf90 <daniele.formichelli@gmail.com>
# Contributor: Tom Richards <tom@tomrichards.net>
# Contributor: Andy Weidenbaum <archbaum@gmail.com>

pkgname=telegram-git
pkgver=r322.b97769b
pkgrel=1
pkgdesc="Telegram messenger CLI"
arch=('x86_64')
url="https://github.com/vysheng/tg"
license=('GPL2')
depends=('libconfig' 'lua')
makedepends=('git')
conflicts=('telegram')
provides=('telegram')
source=("$pkgname::git+https://github.com/vysheng/tg"
        "server.pub")
sha512sums=('SKIP'
            '29a9d6be1a2b6f05ad03e21e7f48067de3892d09dbece55f78d0859b093d6c8ec1b0539379c9021308b5ac6767329c36ae3b173a1051ad2f02de63ba01902282')

pkgver() {
  cd $srcdir/$pkgname

  printf "r%s.%s" $(git rev-list --count HEAD) $(git rev-parse --short HEAD)
}

build() {
  cd $srcdir/$pkgname

  ./configure --prefix=/usr --sbindir=/usr/bin --libexecdir=/usr/bin --sysconfdir=/etc --sharedstatedir=/etc --localstatedir=/etc

  make
}

package() {
  cd $srcdir

  install -Dm644 server.pub $pkgdir/etc/telegram/server.pub
  install -Dm755 $pkgname/telegram $pkgdir/usr/bin/telegram
}
