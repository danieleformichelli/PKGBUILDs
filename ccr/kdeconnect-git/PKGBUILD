# Maintainer:  danyf90 <daniele.formichelli@gmail.com>
# Contributor: Enrique Bravo <sombrahelicopteroATgmailDOTcom>
# Contributor: Kuba Serafinowski <zizzfizzix(at)gmail(dot)com>
# https://github.com/zizzfizzix/pkgbuilds

##############################################################
#### The section below can be adjusted to suit your needs ####
##############################################################

# What type of build do you want?
# See http://techbase.kde.org/Development/CMake/Addons_for_KDE#Buildtypes to check what is supported.
# Default is Release.

_buildtype='Release'

##############################################################

pkgname=kdeconnect-git
pkgver=374.5290189
pkgrel=1
pkgdesc='KDE Connect - kded and kcm'
url='https://albertvaka.wordpress.com/2013/08/05/introducing-kde-connect/'
license=('GPL')
arch=('x86_64')
depends=('kdelibs' 'qjson' 'qca-ossl')
makedepends=('automoc4' 'git' 'cmake')
provides=('kdeconnect')
conflicts=('kdeconnect')
install=kdeconnect.install
screenshot=('http://albertvaka.files.wordpress.com/2013/08/kcm.png')
source=("$pkgname::git://anongit.kde.org/kdeconnect-kde")
sha512sums=('SKIP')

if [[ ! ${_buildtype} == 'Release' ]] && [[ ! ${_buildtype} == 'release' ]]; then
  options=(debug)
fi

pkgver() {
  cd $pkgname
  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  if [[ -e $pkgname-build ]]; then rm -rf $pkgname-build; fi
  mkdir $pkgname-build
}

build() {
  cd $pkgname-build

  cmake -DCMAKE_INSTALL_PREFIX=/usr \
        -DKDE4_BUILD_TESTS=OFF \
        -DCMAKE_BUILD_TYPE=$_buildtype \
        ../$pkgname
  make
}

package() {
  cd $pkgname-build
  make DESTDIR=$pkgdir install
}
