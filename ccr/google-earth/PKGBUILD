# Maintainer:   danyf90 <daniele.formichelli@gmail.com>
# Contributors: Igor Isaias Banlian, 458italia, Madek, Berseker, Syr, Det, AlmAck, Eugen
# Check the latest version with: $ curl -s https://dl.google.com/dl/earth/client/current/google-earth-stable_current_x86_64.rpm | head -c96 | strings | cut -d "-" -f4

# Usage of 32-bit binaries on x86_64 improves stability, set _bin32 to 0 to disable.
_bin32="1"

pkgname=google-earth
pkgver=7.1.2.2041
pkgrel=2
pkgdesc="A 3D interface to view satellite images of Earth and other objects."
arch=('x86_64')
url="https://www.google.com/earth/index.html"
license=('Custom')
depends=('desktop-file-utils' 'lib32-fontconfig' 'hicolor-icon-theme' 'ld-lsb>=3-5' 'lib32-libgl' 'lib32-libsm' 'lib32-libxrender' 'lib32-mesa' 'shared-mime-info' 'xdg-utils')
optdepends=('lib32-catalyst-utils: For AMD Catalyst'
            'lib32-gtk2: SCIM support'
            'lib32-nss-mdns: In case the application fails to contact the servers'
            'lib32-nvidia-utils: For the NVIDIA driver'
            'lib32-nvidia-utils-bumblebee: For the NVIDIA driver + Bumblebee setups'
            'ttf-ms-fonts: Fonts')
options=('!emptydirs')
install=$pkgname.install
source=("$pkgname-stable_${pkgver}_amd64.deb::https://dl.google.com/earth/client/current/$pkgname-stable_current_amd64.deb"
        "$pkgname-mimetypes.xml")
sha512sums=('88bc5faddcf607ee687db5f5d97ba58673846684b3a32d8378dd4d015e9fc7f6fdf6d3386dcf788a29426f334b90b985b8fe36314c587fa78b10e6fc0df393ef'
            'e60cfcac8614877ae46ba8a7e00fc2e7c5d0dbc0a860352665f5703f0ef6a6dae6949f05cf9f41d655673febb609fc42f1f26eaf44385a890e0d05af08ba00d5')

_instdir=/opt/google/earth/free/

package() {
  msg2 "Extracting the data.tar.lzma"
  bsdtar -xf data.tar.lzma -C $pkgdir/

  msg2 "Moving stuff in place"
  # The .desktop
  mv $pkgdir/$_instdir/$pkgname.desktop $pkgdir/usr/share/applications/

  # Icons
  for i in 16 22 24 32 48 64 128 256; do
    install -Dm644 $pkgdir/$_instdir/product_logo_${i}.png $pkgdir/usr/share/icons/hicolor/${i}x${i}/apps/$pkgname.png
  done

  # The MIME package
  install -Dm644 $pkgname-mimetypes.xml $pkgdir/usr/share/mime/packages/$pkgname-mimetypes.xml

  # The license (too many different ones to do this in "source=()")
  install -d $pkgdir/usr/share/licenses/$pkgname/
  curl -s ${url/i*}/license.html -o $pkgdir/usr/share/licenses/$pkgname/license.html

  msg2 "Removing the Debian-intended cron job and duplicated images"
  rm -r $pkgdir/etc/cron.daily/ $pkgdir/$_instdir/product_logo_*.png

#   msg2 "Fixing the coordinates regression"
#   install -m755 googleearth "$pkgdir"/$_instdir/

  # Write the _bin32 value to .install
  if [ "$_bin32" != '1' ]; then
    sed -i "s/_bin32=.*/_bin32=\"0\"/" "$startdir"/$pkgname.install
  else
    sed -i "s/_bin32=.*/_bin32=\"1\"/" "$startdir"/$pkgname.install
  fi
}
