# Maintainer:  danyf90 <daniele.formichelli@gmail.com>
# Contributor: Philipp 'TamCore' B. <philipp [at] tamcore [dot] eu>
# Contributor: Jakub Schmidtke <sjakub-at-gmail-dot-com>
# Contributor: Christoph Brill <egore911-at-gmail-dot-com>
# Contributor: Lubomir 'Kuci' Kucera <kuci24-at-gmail-dot-com>

pkgname=android-studio-canary
_pkgname=android-studio
pkgver=0.8.12
pkgrel=1
_build=135.1503853
pkgdesc="A new Android development environment based on IntelliJ IDEA. Canary branch"
arch=('i686' 'x86_64')
url="http://developer.android.com/sdk/installing/studio.html"
license=('APACHE')
depends=('android-sdk' 'java-environment' 'python')
makedepends=('unzip')
optdepends=('android-sdk-platform-tools' 'android-sdk-build-tools')
provides=("android-studio=$pkgver")
conflicts=('android-studio' 'android-studio-beta' 'android-studio-dev')
options=('!strip')
install=$pkgname.install
source=("http://dl.google.com/dl/android/studio/ide-zips/$pkgver/android-studio-ide-$_build-linux.zip"
        "$_pkgname.desktop")
sha512sums=('9898377c79f4d781a468c21883a9807ac4d05828204911f79ca2345b20bcc6fcecd4d570b4decfdb11674345f34baf38f05ac8b1d87d3b18b9e25ffd34260697'
            'fa3567f328af45d265c4fabf3e41b55d8e8cccfa9675e745f07dd6ae41950dd53a2ef41ef1caee86643f5c2ddf7a7681ee17155e208a7b6fdae6c0537dfc0c94')

if [ $CARCH = "x86_64" ]; then
  depends+=('lib32-fontconfig' 'lib32-libxrender' 'lib32-mesa')
else
  depends+=('fontconfig' 'libxrender' 'mesa')
fi

prepare() {

  cd $srcdir/$_pkgname

  # extract the application icon
  unzip -qo lib/resources.jar artwork/icon_AS_128.png

  # enable anti aliasing
  echo "-Dswing.aatext=true" >> studio.vmoptions
  echo "-Dswing.aatext=true" >> studio64.vmoptions
}

package() {
  cd $srcdir/$_pkgname

  # application stuff
  install -d $pkgdir/{opt/$_pkgname,usr/bin}
  cp -a bin lib plugins $pkgdir/opt/$_pkgname
  ln -s /opt/android-studio/bin/studio.sh $pkgdir/usr/bin/android-studio

  # starter stuff
  install -Dm655 artwork/icon_AS_128.png $pkgdir/usr/share/pixmaps/$_pkgname.png
  install -Dm655 $srcdir/$_pkgname.desktop $pkgdir/usr/share/applications/$_pkgname.desktop

  chmod -R ugo+rX $pkgdir/opt
}